/**************************************************************************************************
 GRADLE CONFIG
 *************************************************************************************************/

if ((new File(gradle.local)).exists()) {
    apply from: gradle.local
} else {
    apply from: gradle.remote
}

applicationScripts.each {
    apply from: it
}

apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'

android {
    namespace(tablesNamespace)
    compileSdk(compileVersion)

    defaultConfig {
        minSdkVersion(minVersion)
        targetSdkVersion(targetVersion)

        testInstrumentationRunner(instrumentationRunner)
    }

    buildFeatures {
        aidl true
        buildConfig true
    }

    flavorDimensions.add("stage")

    productFlavors {
        primary {
            dimension 'stage'
            versionCode(primaryVersionCode)
            versionName(primaryVersion)
        }

        snapshot {
            dimension 'stage'
            versionCode(snapshotVersionCode)
            versionName(snapshotVersion)
        }
    }


    signingConfigs {
        release {
            if (shouldSign) {
                storeFile file(signStoreFilePath)
                storePassword signStorePass
                keyAlias signKeyAlias
                keyPassword signKeyPass
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled(minify)
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'

            if (shouldSign) {
                signingConfig signingConfigs.release
            }
        }
        debug {
            debuggable(true)
            //testCoverageEnabled(testCoverage) // Allows AndroidTest JaCoCo reports to be generated
        }
    }

    packagingOptions {
        exclude 'META-INF/*'
    }

    compileOptions {
        sourceCompatibility javaVersion
        targetCompatibility javaVersion
    }

    publishing {
        singleVariant("snapshotDebug")
        singleVariant("primaryRelease")
    }

    testOptions{
        animationsDisabled = true
        unitTests {
            includeAndroidResources = true
        }
    }
}

configurations.all {
    resolutionStrategy {
        cacheDynamicVersionsFor(0, 'seconds')
        cacheChangingModulesFor(0, 'seconds')
    }
}

allprojects {
    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:deprecation" << "-Xlint:unchecked"
    }
}

dependencies {
    implementation 'org.opendatakit:odkx-rest-interface:2.2.0-SNAPSHOT'

    implementation 'androidx.annotation:annotation:1.7.1'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.fragment:fragment:1.5.7'
    implementation 'androidx.preference:preference:1.1.1'
    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'com.google.android.material:material:1.7.0'
    implementation 'com.github.wbrunette:ProgressWheel:3.4'

    implementation 'com.google.firebase:firebase-analytics:19.0.2'
    implementation 'com.google.firebase:firebase-crashlytics:18.2.3'
    implementation 'com.google.android.material:material:1.7.0'


    if (libraryProjectPath.exists() && gradle.useLocal) { // Local project is favoured
        implementation project(libraryProjectName)
    } else {
        primaryImplementation(group: odkxGroupId, name: libraryPackageName, version: primaryVersion)
        snapshotImplementation(group: odkxGroupId, name: libraryPackageName, version: snapshotVersion)
    }

    if (commonProjectPath.exists() && gradle.useLocal) { // Local project is favoured
        implementation project(commonProjectName)
    } else {
        pirmaryImplementation(group: odkxGroupId, name: commonPackageName, version: primaryVersion)
        snapshotImplementation(group: odkxGroupId, name: commonPackageName, version: snapshotVersion);
    }

    implementation 'com.google.android.gms:play-services-maps:17.0.1'

    //for Espresso
    androidTestImplementation 'androidx.test:runner:1.5.2'
    androidTestImplementation 'androidx.test:rules:1.5.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.5.1'
    androidTestImplementation 'androidx.test.espresso:espresso-web:3.5.1'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.5.1'
    androidTestImplementation 'androidx.annotation:annotation:1.7.1'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'

    //for UI Automator
    androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.2.0'
    androidTestImplementation 'org.hamcrest:hamcrest-integration:1.3'
}

task grantPermissionForODKXApp {
    dependsOn grantServicesReadExternalStoragePermission
    dependsOn grantServicesWriteExternalStoragePermission
}



